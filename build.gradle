apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
    }
}

subprojects {
    apply plugin: 'java'

    defaultTasks 'build'

    group = 'net.svab.mephisto'
    version = '0.1-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    repositories {
        mavenCentral()

        maven {
            name 'mulesoft'
            url "https://repository-master.mulesoft.org/releases/"
        }
    }

    project.ext {
        guavaVersion = '18.0'
        jerseyVersion = '2.16'
        ramlVersion = '0.8.11'
        slf4jVersion = '1.7.10'
        junitVersion = '4.12'
        mockitoVersion = '2.0.4-beta'
        assertjVersion = '1.7.1'
        catchExceptionVersion = '1.3.4'
    }

    jar {
        manifest {
            attributes 'Package': "net.svab.mephisto",
                    'Implementation-Title': "${project.name}",
                    'Implementation-Version': "${version}",
                    'Implementation-Vendor': 'Michal Svab',
                    'Built-By': 'Michal Svab',
                    'Built-Date': new Date().getDateTimeString(),
                    'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
                    'Created-By': 'Java ' + System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"

        options.encoding = 'UTF-8'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
